/**
 * Appcelerator Titanium Mobile
 * Copyright (c) 2009-2019 by Axway. All Rights Reserved.
 * Licensed under the terms of the Apache Public License.
 * Please see the LICENSE included with this distribution for details.
 */

apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// Checks our Java code against our style guidelines and for common coding mistakes using "checkstyle.xml".
// Will trigger a build failure if any violations have been detected.
task checkJavaStyle(type: Checkstyle) {
	source 'src/main/java'
	include '**/*.java'
	classpath = files()
}

// Hook into Java compile task.
tasks.withType(JavaCompile) {
	// Check Java code for mistakes before compiling.
	dependsOn checkJavaStyle

	// Suppress compiler warning "bootstrap class path not set in conjunction with source" which happens when
	// building with JDK newer than the Java version we're targeting. (Ex: Build with JDK 8, but target Java 7.)
	// Note: Build tool wants a reference to runtime JAR of the same version we're targeting. Not going to happen.
	options.compilerArgs << '-Xlint:-options'
}

// Bundle the below library dependencies within this project's built JAR.
// Needed when building modules via "_buildModule.js" which only references the "kroll-apt.jar".
jar {
	manifest {
		attributes 'Main-Class': 'org.appcelerator.kroll.annotations.generator.KrollBindingGenerator'
	}
	from {
		configurations.runtimeClasspath.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}

dependencies {
	implementation 'com.googlecode.json-simple:json-simple:1.1'
	implementation 'org.freemarker:freemarker:2.3.14'
	implementation 'log4j:log4j:1.2.17'
}
